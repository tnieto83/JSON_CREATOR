#ABNF 1.0 ISO8859-1;

// ---------------------------------------------------------------------------
// ASR grammar for Canadian and US telephone numbers
// Martin Tozer - June 2015
// ---------------------------------------------------------------------------

root $phone = ([[$ONE] [dash]{} $SIL* $GRP1 [dash]{} $SIL*] $GRP2 [dash]{} $SIL* $GRP3 [$EXT] | $NONEONE) ;

$ONE =  [plus]{} (one {1}) $SIL* ;

$GRP1 = ($trpl | ($dbl $sngl)| ($sngl $dbl) | ($sngl $sngl $sngl) | $hndrds) $SIL* ;
						
$GRP2 = ($trpl | ($dbl $sngl) | ($sngl $dbl)| ($sngl $sngl $sngl))$SIL* ;

$GRP3 = (($trpl $sngl) | ($sngl $trpl) | ($dbl $dbl) | ($sngl $sngl $sngl $sngl) | ($tens $tens) | ($sngl $sngl $dbl) | ($dbl $sngl $sngl) | ($sngl $dbl $sngl));

$EXT = $SIL* (extension{ext} | ex {ext})
// two digit extensions
		(
(($sngl $sngl) | $tens | $dbl) |
// three digit extensions
		($trpl | ($dbl $sngl) | ($sngl $dbl)| ($sngl $sngl $sngl)) |
// four digit extensions
		(
		($sngl $sngl $sngl $sngl) | ($tens $tens) | ($dbl $dbl) | ($trpl $sngl) | ($sngl $trpl) | ($sngl $sngl $dbl) | ($dbl $sngl $sngl) | ($sngl $dbl $sngl) )|
// five digit extensions
		(
		($sngl $sngl $sngl $sngl $sngl) | ($sngl $trpl $sngl) | ($dbl $trpl) | ($trpl $dbl) | ($sngl $dbl $dbl) | ($dbl $dbl $sngl) | ($dbl $sngl $dbl) | ($dbl $sngl $sngl $sngl) | ($sngl $dbl $sngl $sngl) | ($sngl $sngl $sngl $dbl) | ($sngl $sngl $dbl $sngl) | ($trpl $sngl $sngl) | ($sngl $sngl $trpl)
		)
		);

$NONEONE = ($sngl "one one" {11});

// ---------------------------------------------------------------------------
// auxiliary variables

$SIL =  "[SIL]" {};

$trpl = (
		"triple oh" {000} |
		"triple zero" {000} |
		"triple one" {111} |
		"triple two" {222} |
		"triple three" {333} |
		"triple four" {444} |
		"triple five" {555} |
		"triple six" {666} |
		"triple seven" {777} |
		"triple eight" {888} |
		"triple nine" {999}
		);

$dbl = (
		"double oh" {00} |
		"double zero" {00} |
		"double one" {11} |
		"double two" {22} |
		"double three" {33} |
		"double four" {44} |
		"double five" {55} |
		"double six" {66} |
		"double seven" {77} |
		"double eight" {88} |
		"double nine" {99}
		);

$sngl = (
		oh {0} |
		zero {0} |
		one {1} |
		two {2} |
		three {3} |
		four {4} |
		five {5} |
		six {6} |
		seven {7} |
		eight {8} |
		nine {9}
		);

$hndrds = (
		"eight hundred" {800} |
		"nine hundred" {900}
		);
		
$tens = (
		"ten" {10} |
		"eleven" {11} |
		"twelve" {12} |
		"thirteen" {13} |
		"fourteen" {14} |
		"fifteen" {15} |
		"sixteen" {16} |
		"seventeen" {17} |
		"eighteen" {18} |
		"nineteen" {19} |
		"twenty" {20} |
		"twenty-one" {21} |
		"twenty-two" {22} |
		"twenty-three" {23} |
		"twenty-four" {24} |
		"twenty-five" {25} |
		"twenty-six" {26} |
		"twenty-seven" {27} |
		"twenty-eight" {28} |
		"twenty-nine" {29} |
		"thirty" {30} |
		"thirty-one" {31} |
		"thirty-two" {32} |
		"thirty-three" {33} |
		"thirty-four" {34} |
		"thirty-five" {35} |
		"thirty-six" {36} |
		"thirty-seven" {37} |
		"thirty-eight" {38} |
		"thirty-nine" {39} |
		"forty" {40} |
		"forty-one" {41} |
		"forty-two" {42} |
		"forty-three" {43} |
		"forty-four" {44} |
		"forty-five" {45} |
		"forty-six" {46} |
		"forty-seven" {47} |
		"forty-eight" {48} |
		"forty-nine" {49} |
		"fifty" {50} |
		"fifty-one" {51} |
		"fifty-two" {52} |
		"fifty-three" {53} |
		"fifty-four" {54} |
		"fifty-five" {55} |
		"fifty-six" {56} |
		"fifty-seven" {57} |
		"fifty-eight" {58} |
		"fifty-nine" {59} |
		"sixty" {60} |
		"sixty-one" {61} |
		"sixty-two" {62} |
		"sixty-three" {63} |
		"sixty-four" {64} |
		"sixty-five" {65} |
		"sixty-six" {66} |
		"sixty-seven" {67} |
		"sixty-eight" {68} |
		"sixty-nine" {69} |
		"seventy" {70} |
		"seventy-one" {71} |
		"seventy-two" {72} |
		"seventy-three" {73} |
		"seventy-four" {74} |
		"seventy-five" {75} |
		"seventy-six" {76} |
		"seventy-seven" {77} |
		"seventy-eight" {78} |
		"seventy-nine" {79} |
		"eighty" {80} |
		"eighty-one" {81} |
		"eighty-two" {82} |
		"eighty-three" {83} |
		"eighty-four" {84} |
		"eighty-five" {85} |
		"eighty-six" {86} |
		"eighty-seven" {87} |
		"eighty-eight" {88} |
		"eighty-nine" {89} |
		"ninety" {90} |
		"ninety-one" {91} |
		"ninety-two" {92} |
		"ninety-three" {93} |
		"ninety-four" {94} |
		"ninety-five" {95} |
		"ninety-six" {96} |
		"ninety-seven" {97} |
		"ninety-eight" {98} |
		"ninety-nine" {99}
		);

// ---------------------------------------------------------------------------
